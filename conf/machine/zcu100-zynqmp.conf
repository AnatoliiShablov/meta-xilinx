#@TYPE: Machine
#@NAME: zcu100-zynqmp
#@DESCRIPTION: Machine support for ZCU100 Evaluation Board.
#

require conf/machine/include/tune-zynqmp.inc
require conf/machine/include/machine-xilinx-default.inc
require conf/machine/include/machine-xilinx-board.inc
require conf/machine/include/machine-xilinx-qemu.inc

MACHINE_FEATURES = "rtc ext2 ext3 vfat usbhost qemu-system-xilinx wifi bluetooth"

UBOOT_MACHINE = "xilinx_zynqmp_zcu100_revB_defconfig"

SERIAL_CONSOLE = "115200 ttyPS0"

SERIAL_CONSOLES_CHECK = "${SERIAL_CONSOLES}"

KERNEL_DEVICETREE = "xilinx/zynqmp-zcu100-revB.dtb"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-xlnx"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-xlnx"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "zcu100-power-button wl18xx-fw"

# This machine has a QEMU model, runqemu setup:
# There is no ZCU100 in mainline, use the ZCU102
QB_MACHINE = "-machine xlnx-zcu102"
QB_MEM = "-m 2048"
QB_OPT_APPEND = "-nographic -serial null -serial mon:stdio"
# There is no Ethernet, which means no network for QEMU
QB_TAP_OPT = "-net none"
QB_SLIRP_OPT = "-net none"

# Xilinx QEMU options
QB_OPT_APPEND_append_qemuboot-xilinx = "${@qemuboot_xilinx_hw_devicetrees(d)}"
QB_OPT_APPEND_append_qemuboot-xilinx = "zcu100-arm.dtb "

# Load the boot media
QB_OPT_APPEND_append_qemuboot-xilinx = " -device loader,file=${DEPLOY_DIR_IMAGE}/arm-trusted-firmware.elf,cpu-num=0 \
		-device loader,file=${DEPLOY_DIR_IMAGE}/u-boot.elf \
		"

# Load the kernel image so the user can run 'booti 0x80000 0x6000000 0x4000000' to boot the kernel
QB_DEFAULT_FSTYPE_qemuboot-xilinx = "cpio.gz.u-boot"
QB_ROOTFS_OPT_qemuboot-xilinx = " -device loader,addr=0x6000000,file=@ROOTFS@,force-raw=true"
QB_OPT_APPEND_append_qemuboot-xilinx = " -device loader,addr=0x4000000,file=${DEPLOY_DIR_IMAGE}/${QB_DTB} \
		-device loader,addr=0x80000,file=${DEPLOY_DIR_IMAGE}/${KERNEL_IMAGETYPE} \
		"

# Reset and unhalt CPU0
QB_OPT_APPEND_append_qemuboot-xilinx = "${@qemuboot_xilinx_unhalt(d)}"

# Multi-arch options
QB_OPT_APPEND_append_qemuboot-xilinx-multi = " -machine-path ${DEPLOY_DIR_IMAGE}/qemu-tmp"

QB_OPT_XILINX_PMU_ROM ?= "/not/found"
QB_BACKGROUND_COMMAND_qemuboot-xilinx-multi = "${STAGING_BINDIR_NATIVE}/qemu-xilinx/qemu-system-microblazeel -M microblaze-fdt"
QB_BACKGROUND_COMMAND_append_qemuboot-xilinx-multi = " -display none ${@qemuboot_xilinx_hw_devicetrees(d)}/zynqmp-pmu.dtb"
QB_BACKGROUND_COMMAND_append_qemuboot-xilinx-multi = " -kernel ${QB_OPT_XILINX_PMU_ROM}"
QB_BACKGROUND_COMMAND_append_qemuboot-xilinx-multi = " -device loader,file=${DEPLOY_DIR_IMAGE}/pmu-firmware-${MACHINE}.elf"
QB_BACKGROUND_COMMAND_append_qemuboot-xilinx-multi = " -machine-path ${DEPLOY_DIR_IMAGE}/qemu-tmp"
QB_BACKGROUND_COMMAND_append_qemuboot-xilinx-multi = " -device loader,addr=0xfd1a0074,data=0x1011003,data-len=4 -device loader,addr=0xfd1a007C,data=0x1010f03,data-len=4"
